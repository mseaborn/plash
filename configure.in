dnl Process this file with autoconf to produce a configure script.

AC_INIT([plash], [1.16])
AC_PROG_CC(gcc-4.0 gcc-3.3 gcc)
AC_CONFIG_FILES([src/config.sh])


AC_ARG_WITH([python],
            AC_HELP_STRING([--without-python],
                           [if Python should not be used]),
            [with_python=$withval],
            [with_python=yes])
if test "$with_python" = yes; then
  AC_PATH_PROG([PYTHON],[python])
  if test -z "$PYTHON"; then
    AC_MSG_ERROR([Cannot find python; try --without-python])
  fi

  # Check whether you have distutils, otherwise fail
  AC_MSG_CHECKING([for the distutils Python package])
  ac_distutils_result=`$PYTHON -c "import distutils" 2>&1`
  if test -z "$ac_distutils_result"; then
    AC_MSG_RESULT([yes])
  else
    AC_MSG_RESULT([no])
    AC_MSG_ERROR([cannot import Python module "distutils".
Please check your Python installation, or try --without-python.
The error was:
$ac_distutils_result])
  fi
fi


AC_ARG_WITH([gtk],
            AC_HELP_STRING([--without-gtk],
                           [if Gtk should not be used]),
            [with_gtk=$withval],
            [with_gtk=yes])
if test "$with_gtk" = yes; then
  AC_PATH_PROG(PKG_CONFIG, pkg-config, :)
  AC_SUBST(PKG_CONFIG)
  if test "$PKG_CONFIG" = ":"; then
    AC_MSG_ERROR([pkg-config not found (so can't find Gtk)])
  fi

  AC_MSG_CHECKING([for gtk >= 2.8.0])
  if ! $PKG_CONFIG gtk+-2.0 --exists; then
    AC_MSG_ERROR([gtk not installed; try --without-gtk])
  else
    gtk_version=`$PKG_CONFIG gtk+-2.0 --modversion`
    AC_MSG_RESULT([found gtk $gtk_version])
    if ! $PKG_CONFIG gtk+-2.0 --atleast-version 2.8.0; then
      AC_MSG_ERROR([gtk must be >=2.8.0; try --without-gtk])
    fi
  fi
fi


if test "x$GLIBC_DIR" = x; then
  #GLIBC_DIR=glibc
  GLIBC_DIR=./glibc-i386-objs-2.3.5_2
fi
AC_MSG_CHECKING([for glibc build tree or object files])
if test -d "$GLIBC_DIR"; then
  AC_MSG_RESULT([\"$GLIBC_DIR\" exists])
else
  AC_MSG_ERROR([\"$GLIBC_DIR\" not present])
fi


# We don't need to distinguish between many glibc versions.
# Also, not all versions are distinguishable based on abi-versions.h.
AC_MSG_CHECKING([glibc version for \"$GLIBC_DIR\"])
if grep 'GLIBC_2_4' $GLIBC_DIR/abi-versions.h >/dev/null; then
  AC_MSG_RESULT([>= 2.4])
  GLIBC_VERSION=240
elif grep 'GLIBC_2_3_4' $GLIBC_DIR/abi-versions.h >/dev/null; then
  AC_MSG_RESULT([>= 2.3.4 but < 2.4])
  GLIBC_VERSION=234
else
  AC_MSG_ERROR([unknown])
fi


AC_SUBST(PACKAGE_VERSION)
AC_SUBST(GLIBC_DIR)
AC_SUBST(GLIBC_VERSION)
AC_SUBST(with_gtk)
AC_SUBST(with_python)
AC_OUTPUT
